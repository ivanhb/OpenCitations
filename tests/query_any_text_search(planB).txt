SELECT DISTINCT ?doc ?doi ?title ?year ?author ?author_iri (COUNT(distinct ?cited) AS ?num_cited)
WHERE  { 
  ?lit bds:search 'ESMO-ESGO-ESTRO' . 
  ?lit bds:matchAllTerms 'true' . 
  ?lit bds:relevance ?score .
  ?lit bds:minRelevance '0.4' .
  ?lit bds:maxRank '300' .
 
  {
    ?entry c4o:hasContent ?lit . 
    ?entry biro:references ?iri .
  }
  UNION
  {?iri dcterms:title  ?lit } 
  UNION 
  {?iri fabio:hasSubtitle ?lit} 
  UNION 
  {
    ?myra foaf:familyName ?lit .
	?role pro:isHeldBy ?myra .
	?iri pro:isDocumentContextFor ?role .
  } 
  .
  
  OPTIONAL {
    ?iri rdf:type ?type .
    BIND(?iri as ?doc) .
    OPTIONAL {?doc dcterms:title  ?title .}
    OPTIONAL {?doc fabio:hasSubtitle  ?subtitle .}
    OPTIONAL {?doc fabio:hasPublicationYear ?year .}
    OPTIONAL {?doc cito:cites ?cited .}
    OPTIONAL {
    	?iri datacite:hasIdentifier [
    		datacite:usesIdentifierScheme datacite:doi ;
			literal:hasLiteralValue ?doi 
      	]
  	}
  
  	OPTIONAL {
         ?iri pro:isDocumentContextFor [
             pro:withRole pro:author ;
             pro:isHeldBy ?author_iri 
         ].
         ?author_iri foaf:familyName ?fname .
         ?author_iri foaf:givenName ?name .
         BIND(CONCAT(STR(?name),' ', STR(?fname)) as ?author) .
  	}
  }
}GROUP BY ?doc ?doi ?title ?year ?author ?author_iri